%macro coxps2(data,byvar,time,event,strata,cov,varlist,out);
proc datasets nolist;
delete temp cs pe pe_cs convergencestatus freqs freqs1 freqs2;
run;
quit;
data temp;
 set &data;
 if &cov=. then delete; 
 /*%if %length(&varlist)>0 %then %do;
 if &varlist=. then delete;/** assume one covariate to adjust for **
 %end;*/
run;
proc sort data=temp;
 by &byvar;
run;
proc freq data=temp;
 table &event*&cov;
 by &byvar;
 ods output crosstabfreqs=freqs;
run;
data freqs1;
 set freqs;
 if &cov=0 and &event=1;
 freq0=frequency;
 colp0=colpercent;
 keep &byvar freq0 colp0;
run;
data freqs2;
 set freqs;
 if &cov=1 and &event=1;
 freq1=frequency;
 colp1=colpercent;
 keep &byvar freq1 colp1;
run;
proc phreg data=temp;
 model &time*&event(0)=&cov &varlist/rl;
 strata &strata;
 ods output censoredsummary=cs(keep=&byvar &strata total event);
 ods output parameterestimates=pe(keep=&byvar parameter df estimate stderr chisq probchisq hazardratio hrlowercl hruppercl);
 ods output convergencestatus=convergencestatus;
 by &byvar;
run;
proc sort data=pe;
 by &byvar;
run;
data cs;
 set cs;
 if %do h=1 %to %numwords(&strata)-1;
    %let name=%scan(&strata,&h);
    compress(&name)="" or 
    %end; 
    %let name=%scan(&strata,%numwords(&strata));
    compress(&name)="";
 drop &strata;
run;
proc sort data=cs;
 by &byvar;
run;
proc sort data=convergencestatus;
 by &byvar;
run;
proc sort data=freqs1;
 by &byvar;
run;
proc sort data=freqs2;
 by &byvar;
run;
data &out;
 merge cs pe convergencestatus freqs1 freqs2;
 by &byvar;
run;
 data &out;
 set &out;
 hr_pct=100*(hazardratio-1);
 hrlowercl_pct=100*(hrlowercl-1);
 hruppercl5_pct=100*(hruppercl-1);
run;
proc datasets nolist;
delete temp cs pe pe_cs convergencestatus freqs freqs1 freqs2;
run;
quit;
proc sort data=&out;
 by &byvar;
run;
%mend;
