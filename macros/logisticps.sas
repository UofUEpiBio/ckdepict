%macro logisticps(data,byvar,event,cov,varlist,out);
ods listing close;
proc datasets nolist;
delete temp pe convergencestatus nobs nobs0 nobs1 freqs freqs1 freqs2;
run;
quit;
options nonotes nomprint nomlogic nosymbolgen;
data temp;
 set &data;
 if &cov=. then delete; 
run;
proc sort data=temp;
 by &byvar;
run;
proc freq data=temp;
 table &event*&cov;
 by &byvar;
 ods output crosstabfreqs=freqs;
run;
data freqs1;
 set freqs;
 if &cov=0 and &event=1;
 freq0=frequency;
 colp0=colpercent;
 keep &byvar freq0 colp0;
run;
data freqs2;
 set freqs;
 if &cov=1 and &event=1;
 freq1=frequency;
 colp1=colpercent;
 keep &byvar freq1 colp1;
run;
proc logistic data=temp desc;
 model &event=&cov &varlist;
 ods output parameterestimates=pe(drop=_esttype_);
 ods output convergencestatus=convergencestatus(drop=nullmodel);
 ods output oddsratios=or;
 ods output responseprofile=nobs;
 by &byvar;
run;
data nobs0;
 set nobs;
 if compress(outcome)="0";
 count0=count;
 drop orderedvalue outcome count;
run;
data nobs1;
 set nobs;
 if compress(outcome)="1";
 count1=count;
 drop orderedvalue outcome count;
run;
data or;
 set or;
 rename effect=variable;
 rename oddsratioest=oddsratio lowercl=orlowercl uppercl=oruppercl;
run;
proc sort data=or;
 by &byvar variable;
run;
proc sort data=pe;
 by &byvar variable;
run;
data pe;
 merge pe or;
 by &byvar variable;
run;
proc sort data=pe;
 by &byvar;
run;
proc sort data=convergencestatus;
 by &byvar;
run;
proc sort data=freqs1;
 by &byvar;
run;
proc sort data=freqs2;
 by &byvar;
run;
proc sort data=nobs0;
 by &byvar;
run;
proc sort data=nobs1;
 by &byvar;
run;
data &out;
 merge pe convergencestatus nobs0 nobs1 freqs1 freqs2;
 by &byvar;
run;
data &out;
 set &out;
 oddsratio_pct=100*(oddsratio-1);
 orlowercl_pct=100*(orlowercl-1);
 oruppercl_pct=100*(oruppercl-1);
run;
proc datasets nolist;
delete temp pe convergencestatus nobs nobs0 nobs1 freqs freqs1 freqs2;
run;
quit;
proc sort data=&out;
 by &byvar;
run;
ods listing;
options notes mprint symbolgen mlogic;
%mend;

